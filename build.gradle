subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    sourceCompatibility=JavaVersion.VERSION_1_7

    repositories {
        mavenCentral()
    }

    configurations {
        provided
        functionalTestCompile.extendsFrom testCompile
        functionalTestRuntime.extendsFrom testRuntime
    }

    sourceSets {
        main { compileClasspath += configurations.provided }
        functionalTest {
            java.srcDir file('src/funcTest/java')
            resources.srcDir file('src/funcTest/resources')
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }

    task functionalTest(type: Test) {
        testClassesDir = sourceSets.functionalTest.output.classesDir
        classpath = sourceSets.functionalTest.runtimeClasspath
    }

    dependencies {
        provided group: 'org.projectlombok', name:'lombok', version:"${lombockVersion}"
        testCompile group: 'org.projectlombok', name:'lombok', version:"${lombockVersion}"
    }

    version = '1.0-SNAPSHOT'
    group = "com.eventstech"

    jar {
        manifest.attributes provider: 'eventstech.com.ua'
    }
}
